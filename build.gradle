buildscript {
    apply from: "./script/gradle-script/ext-config.gradle"
    apply from: "./script/gradle-script/ext-version.gradle"
    apply from: "./script/gradle-script/ext-func-allprojects.gradle"
    repositories {
        mavenLocal()
//        maven {
//            url "https://maven.aliyun.com/repository/public-snapshots"
//        }
        maven {
            url "https://maven.aliyun.com/repository/public"
        }
        mavenCentral()
    }
    dependencies {
        classpath(app_plugins['spring-boot-gradle-plugin'])
        classpath(app_plugins['gradle-docker-plugin'])
    }

}


plugins {
    id 'java-library'
    id 'maven-publish'

}

allprojects {
    group 'io.github.kylin-hunter'
//    version '1.0.0-SNAPSHOT'
    version '1.0.2'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    repositories {
        mavenLocal()
        maven {
            url "https://maven.aliyun.com/repository/public"
        }
        mavenCentral()
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'signing'

    apply from: "$rootProject.rootDir/script/gradle-script/ext-func-subprojects.gradle"


    jar {
        manifest.attributes provider: 'gradle'
    }

    dependencies {

        implementation("ch.qos.logback:logback-classic")
        implementation("org.projectlombok:lombok")
        annotationProcessor 'org.projectlombok:lombok'





        testImplementation("org.projectlombok:lombok")
        testAnnotationProcessor 'org.projectlombok:lombok'

        testImplementation('org.springframework.boot:spring-boot-starter-test')

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'

        testImplementation 'org.junit.platform:junit-platform-suite-api:1.8.2'
        testImplementation 'org.junit.platform:junit-platform-suite:1.8.2'
        testImplementation 'org.junit.platform:junit-platform-engine:1.8.2'
        testImplementation 'org.junit.platform:junit-platform-commons:1.8.2'
        testImplementation 'org.junit.platform:junit-platform-launcher:1.8.2'


        testImplementation 'org.junit.vintage:junit-vintage-engine:5.8.2'
        testImplementation(rootProject.dep['jmockit'])

    }

    test {
//        exclude 'com/kylinhunter/**/Suite**'
        useJUnitPlatform()
        testLogging {
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }

    }

    task taskChangeActiveProfile {
        doLast {
            changeActiveProfile(project.profile)
        }
    }
    classes.finalizedBy(taskChangeActiveProfile)
    addDockerBuildTask(project.name)


    java{
        withSourcesJar()
        withJavadocJar()

    }


    // 解决javadoc打包乱码
    javadoc {
        options {
            encoding = "UTF-8"
            charSet = 'UTF-8'
            author = true
            version = true
            locale = "en_US"
        }
        failOnError = false

    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
                pom {

                    name = 'k-file-detector'
                    description = 'a tool for detect file type'
                    url = 'https://github.com/kylin-hunter/k-file-detector'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    // 开发者信息
                    developers {
                        developer {
                            name = 'kylin-hunter'
                            email = 'kylinhunter@163.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/kylin-hunter/k-file-detector'
                        connection = 'scm:git:https://github.com/kylin-hunter/k-file-detector.git'
                        developerConnection = 'scm:git:https://github.com/kylin-hunter/k-file-detector.git'
                    }
                }
            }
        }

        // 定义发布到哪里
        repositories {
            maven {

//                url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = "${sonatypeUsername}"
                    password = "${sonatypePassword}"
                }
            }
        }

    }

    signing {
        sign publishing.publications.mavenJava
    }



}

wrapper {
    gradleVersion = '6.9'
    archiveBase = 'GRADLE_USER_HOME'
    archivePath = 'wrapper/dists'
    distributionBase = 'GRADLE_USER_HOME"'
    distributionPath = 'wrapper/dists'
    distributionUrl = 'https://services.gradle.org/distributions/gradle-6.9-bin.zip'
}
